plugins {
    id 'nebula.project' version '5.1.2'
    id 'nebula.info' version '4.0.3'
    id 'nebula.optional-base' version '4.0.1'
    id 'nebula.release' version '9.0.0'
    id 'nebula.lint' version '10.3.0'

    id 'nebula.maven-publish' version '9.2.0' apply false
    id 'nebula.maven-apache-license' version '9.2.0' apply false
    id 'nebula.source-jar' version '9.2.0' apply false

    id 'org.springframework.boot' version '2.1.0.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.6.RELEASE' apply false
    id 'io.spring.propdeps' version '0.0.9.RELEASE' apply false

    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.dorongold.task-tree' version '1.3.1'
}

ext {
    evcacheVersion = '4.136.0'
    couchbaseVersion = '1.5.19'
}

allprojects {
    apply plugin: 'nebula.release'
    apply plugin: 'nebula.info'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'nebula.lint'

    group 'com.github.aafwu00'

    contacts {
        'aafwu00@gmail.com' {
            moniker 'Taeho Kim'
            role 'Developer'
        }
    }
}

subprojects {
    apply plugin: 'java'

    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    test {
        useJUnitPlatform()
        reports {
            html.enabled = true
        }
    }

    if (!project.name.contains('sample')) {
        apply plugin: 'checkstyle'
        apply plugin: 'findbugs'
        apply plugin: 'pmd'
        apply plugin: 'jacoco'

        apply plugin: 'maven-publish'
        apply plugin: 'nebula.maven-publish'
        apply plugin: 'nebula.source-jar'

        apply plugin: 'com.dorongold.task-tree'
        apply plugin: 'com.github.ben-manes.versions'

        dependencyUpdates.resolutionStrategy = {
            componentSelection { rules ->
                rules.all { ComponentSelection selection ->
                    boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'dev', 'snapshot'].any { qualifier ->
                        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                    }
                    if (rejected) {
                        selection.reject('Release Candidate')
                    }
                }
            }
        }

        checkstyle {
            toolVersion = '8.14'
            ignoreFailures = false
            maxErrors = 0
            maxWarnings = 0
            configFile = rootProject.file('config/checkstyle/checkstyle_checks.xml')
            configProperties = ['checkstyle.header.file'       : "${rootProject.projectDir}/config/checkstyle/java.header",
                                'checkstyle.regexp.header.file': "${rootProject.projectDir}/config/checkstyle/java_regexp.header",
                                'checkstyle.suppressions.file' : "${rootProject.projectDir}/config/checkstyle/suppressions.xml",
                                'checkstyle.importcontrol.file': "${rootProject.projectDir}/config/checkstyle/import-control.xml"]
        }

        findbugs {
            toolVersion = '3.0.1'
            ignoreFailures = false
            effort = 'max'
            reportLevel = 'high'
        }

        pmd {
            toolVersion = '6.9.0'
            ruleSetFiles = rootProject.files('config/pmd/rulesets.xml')
            sourceSets = [project.sourceSets.main as SourceSet]
            ignoreFailures = false
            rulePriority = 3
        }

        jacoco {
            toolVersion = '0.8.2'
        }

        jacocoTestReport {
            reports {
                html.enabled = true
                xml.enabled = true
                csv.enabled = false
            }
        }
    }

    if (project.name.endsWith('sample')) {
        apply plugin: 'org.springframework.boot'
    }

    if (project.name.contains('autoconfigure')) {
        apply plugin: 'propdeps'
        apply plugin: 'propdeps-maven'
        apply plugin: 'propdeps-idea'
        apply plugin: 'propdeps-eclipse'

        compileJava.dependsOn(processResources)
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    configurations {
        all.collect { configuration ->
            configuration.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.SR2'
        }
    }

    dependencies {
        testRuntime 'org.junit.jupiter:junit-jupiter-engine'
        testCompile 'org.junit.jupiter:junit-jupiter-api'
        testCompile 'org.assertj:assertj-core'

        testCompile 'org.springframework.boot:spring-boot-starter-test'

        if (project.name.contains('autoconfigure')) {
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        }
    }
}
