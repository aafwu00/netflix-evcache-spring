plugins {
    id 'nebula.project' version '7.0.9'
    id 'nebula.info' version '7.1.4'
    id 'nebula.optional-base' version '5.0.3'
    id 'nebula.release' version '15.0.0'
    id 'nebula.lint' version '16.8.0'

    id 'nebula.maven-publish' version '17.2.1' apply false
    id 'nebula.maven-apache-license' version '17.2.1' apply false
    id 'nebula.source-jar' version '17.2.1' apply false

    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
    id 'io.spring.propdeps' version '0.0.10.RELEASE' apply false

    id 'com.github.spotbugs' version '4.0.8' apply false

    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.dorongold.task-tree' version '1.5'
}

ext {
    evcacheVersion = '5.15.1'
    spectatorVersion = '0.107.0'
}

allprojects {
    apply plugin: 'nebula.release'
    apply plugin: 'nebula.info'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'nebula.lint'

    group 'com.github.aafwu00'

    contacts {
        'aafwu00@gmail.com' {
            moniker 'Taeho Kim'
            role 'Developer'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = sourceCompatibility

    test {
        useJUnitPlatform()
        reports {
            html.enabled = true
        }
    }

    if (!project.name.contains('sample')) {
        apply plugin: 'checkstyle'
        apply plugin: 'pmd'
        apply plugin: 'jacoco'
        apply plugin: 'com.github.spotbugs'

        apply plugin: 'maven-publish'
        apply plugin: 'nebula.maven-publish'
        apply plugin: 'nebula.source-jar'
        apply plugin: 'nebula.maven-apache-license'

        apply plugin: 'com.dorongold.task-tree'
        apply plugin: 'com.github.ben-manes.versions'

        checkstyle {
            toolVersion = '8.32'
            sourceSets = [project.sourceSets.main as SourceSet]
            configFile = rootProject.file('config/checkstyle/google_checks.xml')
            configProperties = ['checkstyle.regexp.header.file': "${rootProject.projectDir}/config/checkstyle/java_regexp.header"]
            maxErrors = 0
            maxWarnings = 0
        }

        pmd {
            toolVersion = '6.23.0'
            sourceSets = [project.sourceSets.main as SourceSet]
            ruleSets = []
            ruleSetFiles = rootProject.files('config/pmd/rulesets.xml')
            rulePriority = 3
        }

        spotbugs {
            toolVersion = '4.0.2'
            effort = 'max'
            reportLevel = 'high'
            spotbugsTest.enabled = false
        }

        jacoco {
            toolVersion = '0.8.5'
        }

        jacocoTestCoverageVerification {
            violationRules {
                rule {                    
                    limit {
                        minimum = 0.70
                    }
                }
            }
        }

        jacocoTestReport {
            reports {
                html.enabled = true
                xml.enabled = true
                csv.enabled = false
            }
            finalizedBy 'jacocoTestCoverageVerification'
        }

        check {
            finalizedBy 'jacocoTestReport'
        }
    }

    if (project.name.endsWith('sample')) {
        apply plugin: 'org.springframework.boot'
    }

    if (project.name.contains('autoconfigure')) {
        apply plugin: 'propdeps'
        apply plugin: 'propdeps-maven'
        apply plugin: 'propdeps-idea'
        apply plugin: 'propdeps-eclipse'

        compileJava.dependsOn(processResources)
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    configurations {
        all.collect { configuration ->
            configuration.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR3'
        }
    }

    dependencies {
        if (!project.name.contains('sample')) {
            spotbugs 'com.github.spotbugs:spotbugs:4.0.2'
        }
        if (project.name.endsWith('starter')) {
            testImplementation 'org.testcontainers:junit-jupiter:1.13.0'
        }
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        if (project.name.contains('autoconfigure')) {
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        }
    }
}
