buildscript {
    dependencies {
        classpath 'com.google.guava:guava:28.2-jre'// workaround for failing spotbugs
    }
}

plugins {
    id 'nebula.project' version '7.0.4'
    id 'nebula.info' version '5.2.0'
    id 'nebula.optional-base' version '5.0.3'
    id 'nebula.release' version '13.2.1'
    id 'nebula.lint' version '16.0.2'

    id 'nebula.maven-publish' version '14.1.1' apply false
    id 'nebula.maven-apache-license' version '14.1.1' apply false
    id 'nebula.source-jar' version '14.1.1' apply false

    id 'org.springframework.boot' version '2.1.7.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
    id 'io.spring.propdeps' version '0.0.10.RELEASE' apply false

    id 'com.github.spotbugs' version '3.0.0' apply false

    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.dorongold.task-tree' version '1.5'
}

ext {
    evcacheVersion = '5.11.2'
    couchbaseVersion = '1.5.23'
    spectatorVersion = '0.93.0'
}

allprojects {
    apply plugin: 'nebula.release'
    apply plugin: 'nebula.info'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'nebula.lint'

    group 'com.github.aafwu00'

    contacts {
        'aafwu00@gmail.com' {
            moniker 'Taeho Kim'
            role 'Developer'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = sourceCompatibility

    test {
        useJUnitPlatform()
        reports {
            html.enabled = true
        }
    }

    if (!project.name.contains('sample')) {
        apply plugin: 'checkstyle'
        apply plugin: 'pmd'
        apply plugin: 'jacoco'
        apply plugin: 'com.github.spotbugs'

        apply plugin: 'maven-publish'
        apply plugin: 'nebula.maven-publish'
        apply plugin: 'nebula.source-jar'
        apply plugin: 'nebula.maven-apache-license'

        apply plugin: 'com.dorongold.task-tree'
        apply plugin: 'com.github.ben-manes.versions'

        checkstyle {
            toolVersion = '8.19'
            maxErrors = 0
            maxWarnings = 0
            configFile = rootProject.file('config/checkstyle/checkstyle_checks.xml')
            configProperties = ['checkstyle.header.file'       : "${rootProject.projectDir}/config/checkstyle/java.header",
                                'checkstyle.regexp.header.file': "${rootProject.projectDir}/config/checkstyle/java_regexp.header",
                                'checkstyle.suppressions.file' : "${rootProject.projectDir}/config/checkstyle/suppressions.xml",
                                'checkstyle.importcontrol.file': "${rootProject.projectDir}/config/checkstyle/import-control.xml"]
        }

        pmd {
            toolVersion = '6.20.0'
            ruleSetFiles = rootProject.files('config/pmd/rulesets.xml')
            sourceSets = [project.sourceSets.main as SourceSet]
            ignoreFailures = true
            rulePriority = 3
        }

        spotbugs {
            toolVersion = '4.0.0-beta4'
            ignoreFailures = true
            effort = 'max'
            reportLevel = 'high'
        }
        tasks.withType(com.github.spotbugs.SpotBugsTask) {
            reports {
                xml.enabled = false
                html.enabled = true
            }
        }

        jacoco {
            toolVersion = '0.8.5'
        }

        jacocoTestReport {
            reports {
                html.enabled = true
                xml.enabled = true
                csv.enabled = false
            }
        }
    }

    if (project.name.endsWith('sample')) {
        apply plugin: 'org.springframework.boot'
    }

    if (project.name.contains('autoconfigure')) {
        apply plugin: 'propdeps'
        apply plugin: 'propdeps-maven'
        apply plugin: 'propdeps-idea'
        apply plugin: 'propdeps-eclipse'

        compileJava.dependsOn(processResources)
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    configurations {
        all.collect { configuration ->
            configuration.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2'
        }
    }

    dependencies {
        if (!project.name.contains('sample')) {
            spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
        }
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        if (project.name.contains('autoconfigure')) {
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        }
    }
}
